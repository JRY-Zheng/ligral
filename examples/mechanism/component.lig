route Mass(m; a1, F1; a2, F2)
    let g = 9.8;
    # 由于质量块是一个刚体，不会发生变形，
    # 本例中也没有旋转自由度，因此两个连接
    # 点的加速度始终相同。
    a1 - a2 -> `=0`;
    # 根据牛顿定律，F = ma
    (F1 + F2 + m*g)/m -> a2;
    # 输出连接点的力无法判断，令其可变，待求解。
    Variable[F2m] -> F2;
end

route Spring(k; a1, F1; a2, F2)
    # 由于弹簧没有惯性，因此两个连接点所受的合力为0。
    F1 + F2 -> `=0`;
    (a2 - a1) -> Integrator -> Integrator[x];
    # 根据胡克定律，F = -kx。
    -k*x -> F2;
    # 输出连接点的加速度无法判断，令其可变，待求解。
    Variable[a2s] -> a2;
end

route Damper(c; a1, F1; a2, F2)
    # 由于弹簧没有惯性，因此两个连接点所受的合力为0。
    F1 + F2 -> `=0`;
    (a2 - a1) -> Integrator[v];
    # 根据阻尼器物理性质，F = -cv。
    -c*v -> F2;
    # 输出连接点的加速度无法判断，令其可变，待求解。
    Variable[a2d] -> a2;
end

route Ground(; a1, F1; a2, F2)
    # 地面的加速度始终为0。
    a1 -> `=0`;
    0 -> a2;
    # 地面受力未知，令其可变，待求解。
    Variable[F2g] -> F2;
end
